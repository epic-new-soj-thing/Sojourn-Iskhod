#define BRUISED_2_EFFICIENCY 80
#define BROKEN_2_EFFICIENCY 50
#define DEAD_2_EFFICIENCY 0

//Has processes for all internal organs, called from /mob/living/carbon/human/Life()

/mob/living/carbon/human/proc/process_internal_organs() //Calls all of the internal organ processes
	if(should_have_process(OP_EYES))	//Bad way to do this, should be reworked to call procs from a list that's generated by the species.has_process list + extra processes gotten from organs (carrion)
		eye_process()
	if(should_have_process(OP_KIDNEY_LEFT) || should_have_process(OP_KIDNEY_RIGHT))
		kidney_process()
	if(should_have_process(OP_LIVER))
		liver_process()
	if(should_have_process(OP_HEART))
		heart_process()
	if(should_have_process(OP_LUNGS))
		lung_process()
	if(should_have_process(OP_STOMACH))
		stomach_process()
	if(should_have_process(BP_BRAIN))  // Add brain processing
		brain_process()
	if(is_carrion(src))
		carrion_process()

/mob/living/carbon/human/proc/get_organ_efficiency(process_define)
	var/list/process_list = internal_organs_by_efficiency[process_define]
	var/effective_efficiency = 0
	if(process_list && process_list.len)
		for(var/organ in process_list)
			var/obj/item/organ/internal/I = organ
			effective_efficiency += I.get_process_efficiency(process_define)

	return effective_efficiency ? effective_efficiency : 1

/mob/living/carbon/human/get_specific_organ_efficiency(process_define, parent_organ_tag)
	var/effective_efficiency = 0
	var/obj/item/organ/external/parent_organ
	if(isorgan(parent_organ_tag))
		parent_organ = parent_organ_tag
	else
		parent_organ = get_organ(parent_organ_tag)
	if(parent_organ)
		for(var/organ in parent_organ.internal_organs)
			var/obj/item/organ/internal/I = organ
			if(process_define in I.organ_efficiency)
				effective_efficiency += I.get_process_efficiency(process_define)

	return effective_efficiency ? effective_efficiency : 1

/mob/living/carbon/human/proc/eye_process()
	var/eye_efficiency = get_organ_efficiency(OP_EYES) * (1 + chem_effects[CE_EYEHEAL]) //Highest this goes is 2

	if(eye_efficiency < BRUISED_2_EFFICIENCY)
		eye_blurry = 1
	if(eye_efficiency < BROKEN_2_EFFICIENCY)
		eye_blind = 1
	//update_client_colour()

/mob/living/carbon/human/proc/kidney_process()
	var/kidneys_efficiency = get_organ_efficiency(OP_KIDNEYS) * (1 + chem_effects[CE_PURGER])
	var/obj/item/organ/internal/kidney = random_organ_by_process(OP_KIDNEYS)
	var/chem_toxicity = chem_effects[CE_BLOODCLOT] + chem_effects[CE_SPEEDBOOST]
	var/toxin_strength = chem_effects[CE_TOXIN] * IORGAN_KIDNEY_TOX_RATIO + chem_toxicity

	// Existing damage is subtracted to prevent weaker toxins from maxing out tox wounds on the organ
	var/toxin_damage = kidney ? (toxin_strength / (stats.getPerk(PERK_BLOOD_OF_LEAD) ? 2 : 1)) - (kidneys_efficiency / 100) - kidney.damage * 2 : 0

	// Organ functions
	// Blood regeneration if there is some space
	regenerate_blood(0.2 + 2 * chem_effects[CE_BLOODRESTORE] * (kidneys_efficiency / 100))

	// Bad stuff
	if(kidneys_efficiency < BROKEN_2_EFFICIENCY)
		if(toxin_strength > 0)
			apply_damage(toxin_strength, TOX)	// If your kidneys aren't working, your body will start to take damage

	if(toxin_damage > 0 && kidney)
		kidney.take_damage(toxin_damage, TOX)

/mob/living/carbon/human/proc/liver_process()
	var/liver_efficiency = get_organ_efficiency(OP_LIVER) * (1 + chem_effects[CE_ANTITOX])
	var/obj/item/organ/internal/liver = random_organ_by_process(OP_LIVER)
	var/alcohol_strength = chem_effects[CE_ALCOHOL]
	var/toxin_strength = chem_effects[CE_TOXIN] * IORGAN_LIVER_TOX_RATIO + chem_effects[CE_ALCOHOL_TOXIC]

	// Toxin damage to liver organ - more damage when liver is less efficient at filtering
	var/toxin_damage = 0
	if(liver && toxin_strength > 0)
		// Base damage from toxin strength
		var/base_damage = toxin_strength / (stats.getPerk(PERK_BLOOD_OF_LEAD) ? 2 : 1)

		// Liver efficiency reduces its ability to handle toxins - less efficient = more damage
		var/efficiency_multiplier = max(0.5, (100 - liver_efficiency) / 100 + 0.5) // 0.5x to 1.5x damage

		toxin_damage = base_damage * efficiency_multiplier - liver.damage * 0.5
		toxin_damage = max(0, round(toxin_damage)) // Always at least 1 damage, no decimals

	// Check if liver is dead - dead livers cause systematic organ failure
	if(!liver || (liver.status & ORGAN_DEAD))
		// Dead liver can't process toxins and generates waste products
		if(liver)
			add_chemical_effect(CE_TOXIN, 3) // Increased base toxin buildup from liver failure
			if(prob(8)) // Increased chance for more severe toxin buildup
				add_chemical_effect(CE_TOXIN, 2) // Increased additional toxin buildup
			if(prob(20))
				to_chat(src, SPAN_WARNING("You feel nauseous as waste builds up in your system..."))

			// Dead liver causes constant toxin damage to the player
			var/dead_liver_damage = 1 + (chem_effects[CE_TOXIN] * 0.3) // Base 2 + scaling with toxin level
			apply_damage(dead_liver_damage, TOX)

			if(prob(10))
				to_chat(src, SPAN_DANGER("Your dead liver is poisoning your body!"))

			// Dead liver + existing toxins = systemic organ damage (lowered threshold)
			if(toxin_strength > 2) // Lowered from 5 to 2 - dead liver makes any toxins dangerous
				var/list/vulnerable_organs = list()
				for(var/obj/item/organ/internal/I in internal_organs)
					if(I != liver && !BP_IS_ROBOTIC(I) && !(I.status & ORGAN_DEAD))
						vulnerable_organs += I

				if(vulnerable_organs.len)
					// Damage multiple organs when liver is dead and toxins are high
					var/organs_to_damage = min(vulnerable_organs.len, max(1, round(toxin_strength / 4))) // Reduced divisor from 8 to 4
					for(var/i = 1 to organs_to_damage)
						var/obj/item/organ/internal/target = pick_n_take(vulnerable_organs)
						var/damage_amount = rand(5, 10) // Increased damage (reduced divisor from 10 to 6)
						target.take_damage(damage_amount, TOX)

						if(prob(15))
							to_chat(src, SPAN_DANGER("Your [target.name] is being poisoned by unfiltered toxins!"))

					if(toxin_strength > 8 && prob(8)) // Lowered threshold from 15 to 8, increased chance
						to_chat(src, SPAN_DANGER("Without a functioning liver, toxins are destroying your organs!"))

		// Dead liver doesn't filter toxins or process nutrients
		// Blood loss makes you lose nutriments faster without a liver
		var/blood_volume = get_blood_volume()
		if(blood_volume * effective_blood_volume < total_blood_req + BLOOD_VOLUME_SAFE_MODIFIER)
			if(nutrition >= 300)
				adjustNutrition(-15) // Faster nutrition loss without liver
			else if(nutrition >= 200)
				adjustNutrition(-5)
		return // Exit early - dead liver does nothing else

	// Active toxin filtering - healthy liver removes toxins from the system (but slowly)
	if(chem_effects[CE_TOXIN] > 0 && liver_efficiency > 0)
		// Liver filters toxins based on efficiency - much slower now
		// Base filtering rate: 0.3% of current toxins per tick at 100% efficiency
		var/filter_rate = (liver_efficiency / 100) * 0.003

		// Enhanced filtering with antitox effects
		filter_rate *= (1 + chem_effects[CE_ANTITOX] * 0.3)

		// Calculate how much toxin to remove
		var/toxin_filtered = chem_effects[CE_TOXIN] * filter_rate

		// Minimum filtering - even damaged livers remove some toxins (but very little)
		if(liver_efficiency >= DEAD_2_EFFICIENCY)
			toxin_filtered = max(toxin_filtered, 0.02) // Minimum 0.02 toxin removal per tick

		// Apply toxin filtering
		if(toxin_filtered > 0)
			add_chemical_effect(CE_TOXIN, -toxin_filtered)

			// Liver takes strain from heavy filtering
			if(chem_effects[CE_TOXIN] > 5 && liver_efficiency < 90)
				if(prob(2))
					to_chat(src, SPAN_NOTICE("You feel your liver working hard to process toxins..."))

	// Baseline toxin effects - toxins always cause some damage
	if(toxin_strength > 0)
		// Base toxin damage affects everyone, regardless of liver health
		var/baseline_damage = toxin_strength * 0.15 // 15% of toxin strength as baseline damage

		// Liver efficiency reduces but doesn't eliminate baseline damage
		var/efficiency_reduction = (liver_efficiency / 100) * 0.7 // Max 70% reduction at 100% efficiency
		baseline_damage *= (1 - efficiency_reduction)

		if(baseline_damage > 0.05) // Apply if meaningful
			apply_damage(baseline_damage, TOX)

		if(baseline_damage > 0.3 && prob(3))
			to_chat(src, SPAN_WARNING("You feel the effects of toxins in your system..."))

	// Bad stuff
	// Additional toxin damage when liver efficiency is compromised
	if(liver_efficiency < 90) // Lowered threshold further
		// Toxin effects scale with liver damage
		var/toxin_severity = (90 - liver_efficiency) / 90 // 0 to 1 scale

		if(alcohol_strength)
			toxin_damage += 0.5 * max(2 - (liver_efficiency * 0.01), 0) * alcohol_strength

		if(toxin_strength > 0)
			// Additional toxin damage to player, scaled by severity
			var/extra_damage = toxin_strength * toxin_severity * 0.4 // Increased multiplier
			if(extra_damage > 0.1) // Only apply if meaningful
				apply_damage(extra_damage, TOX)

				if(prob(8) && toxin_severity > 0.3)
					to_chat(src, SPAN_WARNING("You feel sick as toxins overwhelm your compromised liver..."))

	if(toxin_damage > 0 && liver)
		liver.take_damage(toxin_damage, TOX)

	// Blood loss or liver damage make you lose nutriments
	var/blood_volume = get_blood_volume()
	if(blood_volume * effective_blood_volume < total_blood_req + BLOOD_VOLUME_SAFE_MODIFIER || (liver_efficiency < BRUISED_2_EFFICIENCY))
		if(nutrition >= 300)
			adjustNutrition(-10)
		else if(nutrition >= 200)
			adjustNutrition(-2)


/mob/living/carbon/human/proc/heart_process()
	handle_pulse()
	handle_heart_blood()

/mob/living/carbon/human/proc/handle_pulse()
	var/roboheartcheck = TRUE //Check if all hearts are robotic
	for(var/obj/item/organ/internal/vital/heart in organ_list_by_process(OP_HEART))
		if(!BP_IS_ROBOTIC(heart))
			roboheartcheck = FALSE
			break

	if(stat == DEAD || roboheartcheck)
		pulse = PULSE_NONE	//that's it, you're dead (or your metal heart is), nothing can influence your pulse
		return

	if(life_tick % 5 == 0)//update pulse every 5 life ticks (~1 tick/sec, depending on server load)
		pulse = PULSE_NORM

		if(get_blood_volume() * effective_blood_volume <= total_blood_req + BLOOD_VOLUME_BAD_MODIFIER)  //how much blood do we have
			pulse  = PULSE_THREADY  //not enough :(

		if(status_flags & FAKEDEATH || chem_effects[CE_NOPULSE])
			pulse = PULSE_NONE		//pretend that we're dead. unlike actual death, can be inflienced by meds

		pulse = CLAMP(pulse + chem_effects[CE_PULSE], PULSE_SLOW, PULSE_2FAST)

/mob/living/carbon/human/proc/handle_heart_blood()
	var/heart_efficiency = get_organ_efficiency(OP_HEART)
	var/blood_oxygenation = 0.4 * chem_effects[CE_OXYGENATED]
	var/blood_volume = get_blood_volume() // Percentage.

	// Damaged heart virtually reduces the blood volume, as the blood isn't being pumped properly anymore.
	if(heart_efficiency <= 100)	//flat scaling up to 100
		effective_blood_volume = (heart_efficiency / 100) + blood_oxygenation
	else	//half scaling at over 100
		effective_blood_volume = 1 + ((heart_efficiency - 100) / 200) + blood_oxygenation
	blood_volume *= effective_blood_volume

	//Effects of bloodloss
	var/blood_safe = total_blood_req + BLOOD_VOLUME_SAFE_MODIFIER
	var/blood_okay = total_blood_req + BLOOD_VOLUME_OKAY_MODIFIER
	var/blood_bad = total_blood_req + BLOOD_VOLUME_BAD_MODIFIER

	if(blood_volume < total_blood_req)
		status_flags |= BLEEDOUT
		if(prob(15))
			to_chat(src, SPAN_WARNING("Your organs feel extremely heavy"))
	else
		status_flags &= ~BLEEDOUT

	if(blood_volume < 1)
		eye_blurry = max(eye_blurry,6)
		adjustOxyLoss(20)
		if(prob(15))
			to_chat(src, SPAN_WARNING("You feel [pick("extremely tired","terribly weak","the world fade around you")]"))
	else if(blood_volume < blood_bad)
		eye_blurry = max(eye_blurry,6)
		adjustOxyLoss(10)
		if(prob(15))
			to_chat(src, SPAN_WARNING("You feel very [pick("dizzy","woosey","faint")]"))
	else if(blood_volume < blood_okay)
		eye_blurry = max(eye_blurry,6)
		adjustOxyLoss(5)
		if(prob(15))
			Weaken(rand(1,3))
			to_chat(src, SPAN_WARNING("You feel [pick("dizzy","woosey","faint")]"))
	else if(blood_volume < blood_safe)
		if(prob(1))
			to_chat(src, SPAN_WARNING("You feel slightly [pick("dizzy","woosey","faint")]"))
		if(getOxyLoss() < 10)
			adjustOxyLoss(1)

	// Blood loss or heart damage make you lose nutriments
	if(blood_volume < blood_safe || heart_efficiency < BRUISED_2_EFFICIENCY)
		if(nutrition >= 300)
			adjustNutrition(-10)
		else if(nutrition >= 200)
			adjustNutrition(-2)


/mob/living/carbon/human/proc/brain_process()
	// Handle brain hypoxia damage from heart failure
	// Only process if we have an organic brain
	if(!should_have_process(BP_BRAIN))
		return

	var/obj/item/organ/internal/vital/brain/brain_organ = random_organ_by_process(BP_BRAIN)
	if(!brain_organ || BP_IS_ROBOTIC(brain_organ))
		return

	// Apply brain damage from hypoxia when heart stops OR oxygen loss is below 100
	if(pulse == PULSE_NONE || getOxyLoss() >= 100)
		// Calculate brain damage based on oxygen loss percentage
		var/oxygen_loss = getOxyLoss()
		var/damage_amount = 2 // Base damage when heart stops (increased from 1)

		if(oxygen_loss >= 100)
			// Scale damage based on oxygen loss: 100 oxyloss = 10 damage, 150 oxyloss = 15 damage, 200 oxyloss = 20 damage, etc.
			damage_amount = (oxygen_loss / 100) * 10

		// Directly add to brainloss variable instead of using organ damage
		brainloss = min(brainloss + damage_amount, 200) // Cap at 200 (max brain damage)
		if(prob(50))
			to_chat(src, SPAN_DANGER("Your brain is starved of oxygen!"))


/mob/living/carbon/human/proc/lung_process()
	var/base_lung_efficiency = get_organ_efficiency(OP_LUNGS)
	var/toxin_penalty = min(chem_effects[CE_TOXIN] * 2.5, 35) // Toxins affect lung performance
	var/lung_efficiency = max(0, base_lung_efficiency - toxin_penalty)
	var/internal_oxygen = 100 - oxyloss

	internal_oxygen *= lung_efficiency / 100

	if(internal_oxygen < total_oxygen_req)
		if(prob(1))
			Weaken(1.5 SECONDS)
			visible_message(SPAN_WARNING("[src] falls to the ground and starts hyperventilating!."), SPAN_DANGER("AIR! I NEED MORE AIR!"))
			var/i
			for(i = 1; i <= 5; i++)	//gasps 5 times
				spawn(i)
					emote("gasp")

		if(prob(2))
			spawn emote("me", 1, "coughs up blood!")
			drip_blood(10)

		if(prob(4))
			spawn emote("me", 1, "gasps for air!")
			losebreath += 15

		if(prob(15))
			var/heavy_spot = pick("chest", "skin", "brain")
			to_chat(src, SPAN_WARNING("Your [heavy_spot] feels too heavy for your body"))

/mob/living/carbon/human/proc/stomach_process()
	var/stomach_efficiency = get_organ_efficiency(OP_STOMACH)
	//max_nutrition = MOB_BASE_MAX_HUNGER * (stomach_efficiency / 100) - This messes with genetics, and a few perks/affects.
	//If we have for some reason negitive max_nutrition, set to 0 as not to ruin maths in human_movement.dm
	if(-1 >= max_nutrition)
		max_nutrition = 0

	if(nutrition > 0 && stat != 2)
		if(stomach_efficiency <= 0)
			nutrition = 0
		else
			adjustNutrition(-(total_nutriment_req * (stomach_efficiency/100)))

/* We dont have vore
	if(stomach_efficiency <= 1)
		for(var/mob/living/M in stomach_contents)
			M.loc = loc
			stomach_contents.Remove(M)
			continue
		ingested.trans_to_turf(get_turf(src))
*/

/mob/living/carbon/human/var/carrion_stored_chemicals = 0
/mob/living/carbon/human/var/carrion_hunger = 0
/mob/living/carbon/human/var/carrion_last_hunger = -2 MINUTES
/mob/living/carbon/human/proc/carrion_process()
	var/vessel_efficiency = get_organ_efficiency(OP_CHEMICALS)
	var/maw_efficiency = get_organ_efficiency(OP_MAW)
	if(vessel_efficiency)
		carrion_stored_chemicals = min(carrion_stored_chemicals + (0.01 * vessel_efficiency), 0.5 * vessel_efficiency)

	if((maw_efficiency > 1 )&& (world.time > (carrion_last_hunger + 2 MINUTES)))
		var/max_hunger = round(10 * (maw_efficiency / 100))
		if(carrion_hunger < max_hunger)
			carrion_hunger = min(carrion_hunger + (round(1* (maw_efficiency / 100))), max_hunger)
		else
			to_chat(src, SPAN_WARNING("Your hunger is restless!"))
		carrion_last_hunger = world.time

#undef BRUISED_2_EFFICIENCY
#undef BROKEN_2_EFFICIENCY
#undef DEAD_2_EFFICIENCY
